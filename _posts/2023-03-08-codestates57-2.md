---
title: Spring Rest Docs
excerpt:  API 문서화
categories: Spring
tags: [codestates, Java, Spring]
toc: true
toc_sticky: true
author_profile: true
sidebar_main: true
published : false
---

# Spring Rest Docs
- Controller의 슬라이스 테스트를 통해 테스트가 통과 되어야지만 API 문서가 정상적으로 만들어 짐

## API 문서 생성 흐름
1. 테스트 코드 작성  
   - 슬라이스 테스트 코드 작성  
   - API 스펙 정보(Request Body, Response Body, Query Parameter 등) 코드 작성
2. test 태스크(task) 실행  
   - 작성된 슬라이스 테스트 코드 
     - 하나의 테스트 클래스를 실행시켜도 되지만 일반적으로 Gradle의 빌드 태스크(task)중 하나인 test task를 실행 시켜서 API 문서 스니핏(snippet)을 일괄 생성  
   - 테스트 실행 결과가 “passed”면 다음 작업 진행, “failed”면 테스트 케이스를 수정하여 문제를 해결한 후, 다시 테스트 진행
3. API 문서 스니핏(.adoc 파일) 생성
   -  테스트 케이스의 테스트 실행 결과가 “passed”면 테스트 코드에 포함된 API 스펙 정보 코드를 기반으로 API 문서 스니핏이 .adoc 확장자를 가진 파일로 생성됨
    > ### 스니핏(snippet)
    > - 문서의 일부 조각
    > - 테스트 케이스 하나 당 하나의 스니핏이 생성 
    > - 여러개의 스니핏을 모아서 하나의 API 문서를 생성 가능
4. API 문서 생성
   - 생성된 API 문서 스니핏을 모아서 하나의 API 문서로 생성
5. API 문서를 HTML로 변환
   - 생성된 API 문서를 HTML 파일로 변환
   - HTML로 변환된 API 문서는 HTML 파일 자체를 공유할 수도 있고, URL을 통해 해당 HTML에 접속해서 확인 가능

## Spring Rest Docs 설정
- ### build.gradle 설정
```
plugins {
	id 'org.springframework.boot' version '2.7.1'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id "org.asciidoctor.jvm.convert" version "3.3.2"    // (1)
	id 'java'
}

group = 'com.codestates'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

// (2)
ext {
	set('snippetsDir', file("build/generated-snippets"))
}

// (3)
configurations {
	asciidoctorExtensions
}

dependencies {
        // (4)
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
  
        // (5) 
	asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor'

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	implementation 'org.mapstruct:mapstruct:1.5.1.Final'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.1.Final'
	implementation 'org.springframework.boot:spring-boot-starter-mail'

	implementation 'com.google.code.gson:gson'
}

// (6)
tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

// (7)
tasks.named('asciidoctor') {
	configurations "asciidoctorExtensions"
	inputs.dir snippetsDir
	dependsOn test
}

// (8)
task copyDocument(type: Copy) {
	dependsOn asciidoctor            // (8-1)
	from file("${asciidoctor.outputDir}")   // (8-2)
	into file("src/main/resources/static/docs")   // (8-3)
}

build {
	dependsOn copyDocument  // (9)
}

// (10)
bootJar {
	dependsOn copyDocument    // (10-1)
	from ("${asciidoctor.outputDir}") {  // (10-2)
		into 'static/docs'     // (10-3)
	}
}
```

- (1) : .adoc 파일 확장자를 가지는 AsciiDoc 문서를 생성해주는 Asciidoctor를 사용하기 위한 플러그인 추가  
- (2) : ext 변수의 set() 메서드를 이용해서 API 문서 스니핏이 생성될 경로 지정  
- (3) : AsciiDoctor에서 사용되는 의존 그룹 지정
   - :asciidoctor task가 실행되면 내부적으로 (3)에서 지정한 ‘asciidoctorExtensions’라는 그룹을 지정
- (4) :  'org.springframework.restdocs:spring-restdocs-mockmvc'를 추가함으로써 spring-restdocs-core와 spring-restdocs-mockmvc 의존 라이브러리 추가
- (5) :  spring-restdocs-asciidoctor 의존 라이브러리 추가
   - (3)에서 지정한 asciidoctorExtensions 그룹에 의존 라이브러리가 포함
- (6) :  :test task 실행 시, API 문서 생성 스니핏 디렉토리 경로 설정
- (7) :  :asciidoctor task 실행 시, Asciidoctor 기능을 사용하기 위해 :asciidoctor task에 asciidoctorExtensions 을 설정
- (8) : :build task 실행 전에 실행되는 task
  - :copyDocument task가 수행되면 index.html 파일이 src/main/resources/static/docs 에 copy 되며, copy된 index.html 파일은 API 문서를 파일 형태로 외부에 제공하기 위한 용도로 사용 가능
  - (8-1) : :asciidoctor task가 실행된 후에 task가 실행 되도록 의존성 설정
  - (8-2) : "build/docs/asciidoc/" 경로에 생성되는 index.html을 copy한 후,
  - (8-3) :  "src/main/resources/static/docs" 경로로 index.html을 추가
- (9) : :build task가 실행되기 전에 :copyDocument task가 먼저 수행 되도록 함
- (10) : 애플리케이션 실행 파일이 생성하는 :bootJar task 설정
  - (10-1) : :bootJar task 실행 전에 :copyDocument task가 실행 되도록 의존성 설정
  - (10-2, 10-3) :  Asciidoctor 실행으로 생성되는 index.html 파일을 jar 파일 안에 추가
    - jar 파일에 index.html을 추가해 줌으로써 웹 브라우저에서 접속(http://localhost:8080/docs/index.html) 후, API 문서를 확인 가능

### API 문서 스니핏을 사용하기 위한 템플릿(또는 source 파일) 생성
- API 문서 스니핏이 생성 되었을 때 이 스니핏을 사용해서 최종 API 문서로 만들어 주는 템플릿 문서(index.adoc)를 생성해야 함
    - Gradle 기반 프로젝트에서는 아래 경로에 해당하는 디렉토리를 생성해주어야 함
        - src/docs/asciidoc/
    - src/docs/asciidoc/ 디렉토리 내에 비어있는 템플릿 문서(index.adoc)를 생성